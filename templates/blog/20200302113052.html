<h2>m個の入力をn個の出力に変換する</h2>

m個の入力をn個の出力に変換する方法を解説します。

ディープラーニングでは、複数の入力と複数の出力を、多段階に連続的につなぎ、最終的な出力を得ることをします。

このとき、m個の入力から、n個の出力を得る必要があります。

たとえば、4つの入力に対して、5つの出力を得る。5つの入力から、3つの出力を得るということを行います。

<pre>
(x0, x1, x2, x3)
↓
(y0, y1, y2, y3, y4)
↓
(z0, z1, z2)
</pre>

<h3>二つの入力から三つの出力を得る</h3>

では、簡単な例として、二つの入力から三つの出力を得る方法を記述してみましょう。まずPerlのコードで書きます。

<pre>
use strict;
use warnings;

my $x0 = 0.5;
my $x1 = 0.8;

my $w00 = 0.1;
my $w01 = 0.4;
my $w10 = 0.2;
my $w11 = 0.5;
my $w20 = 0.3;
my $w21 = 0.6;

my $b0 = 0.3;
my $b1 = 0.9;
my $b2 = 0.5;

my $y0 = ($w00 * $x0) + ($w01 * $x1) + $b0;
my $y1 = ($w10 * $x0) + ($w11 * $x1) + $b1;
my $y2 = ($w20 * $x0) + ($w21 * $x1) + $b2;

print "($y0, $y1, $y2)\n";
</pre>

出力結果は以下のようになります。

<pre>
(0.67, 1.4, 1.13)
</pre>

このような計算を行うことで、($x1, $x2)という二つの入力値から($y0, $y1, $y2)という三つの出力を得ることができました。

定数パラメーター「$w」は0～1の値、入力値「$x0, $x1」も0～1の値、定数項「$b0, $b1, $b2」も0～1の値と考えてください。これは、確率を、0%から100%で表現していると考えてください。また計算資源の最適化において、float型で扱える値であると考えてください。

ディープラーニングにおいては、最終出力は、合計が1になる、複数の0～1の確率値です。たとえば「眼鏡をかけている確率 0.90(90%)」「眼鏡をかけていない確率 0.10(10%)」となります。

<h3>yの値を0～1に抑えるには?</h3>

計算して試してみると、yの値は、0～1の範囲に収まらないですね。出力を0～1に抑えるには、どうすればよいでしょうか。

これは、次のような関数をyの値に適用すればよいでしょう。

<ul>
  <li>出力結果が、どのような値であっても、0～1の間に変換してくれる関数</li>
</ul>

このような関数の一つとして、以下の関数があります。このような関数は活性化関数と呼ばれていますので、名前はactivateとします。

<pre>
sub activate {
  my ($y) = @_;
  
  my $activate_y = 1 / (1 + exp(-$y));
  
  return $activate_y;
}
</pre>

先のサンプルに活性化関数を適用してみましょう。

<pre>
use strict;
use warnings;

my $x0 = 0.5;
my $x1 = 0.8;

my $w00 = 0.1;
my $w01 = 0.4;
my $w10 = 0.2;
my $w11 = 0.5;
my $w20 = 0.3;
my $w21 = 0.6;

my $b0 = 0.3;
my $b1 = 0.9;
my $b2 = 0.5;

my $y0 = ($w00 * $x0) + ($w01 * $x1) + $b0;
my $y1 = ($w10 * $x0) + ($w11 * $x1) + $b1;
my $y2 = ($w20 * $x0) + ($w21 * $x1) + $b2;

$y0 = activate($y0);
$y1 = activate($y1);
$y2 = activate($y2);

print "($y0, $y1, $y2)\n";

sub activate {
  my ($y) = @_;
  
  my $activate_y = 1 / ( 1 + exp(-$y));
  
  return $activate_y;
}
</pre>

出力結果は以下のようになります。0～1の間に値が収まりましたね。

<pre>
(0.661503159202952, 0.802183888558582, 0.755838899094797)
</pre>

二つの0～1の$xの入力が、三つの0～1の$yの出力を得ることができました。

変数の数を増やしたり、減らしたりすれば、m個の入力をn個の出力に変換するというのは、容易にできますね。

<h3>m個の入力をn個の出力に変換する処理の一般化</h3>

m個の入力をn個の出力に変換する処理を一般化してみましょう。もう一度、下の処理をじっくりみてください。

<pre>
use strict;
use warnings;

my $x0 = 0.5;
my $x1 = 0.8;

my $w00 = 0.1;
my $w01 = 0.4;
my $w10 = 0.2;
my $w11 = 0.5;
my $w20 = 0.3;
my $w21 = 0.6;

my $b0 = 0.3;
my $b1 = 0.9;
my $b2 = 0.5;

my $y0 = ($w00 * $x0) + ($w01 * $x1) + $b0;
my $y1 = ($w10 * $x0) + ($w11 * $x1) + $b1;
my $y2 = ($w20 * $x0) + ($w21 * $x1) + $b2;

$y0 = activate($y0);
$y1 = activate($y1);
$y2 = activate($y2);

print "($y0, $y1, $y2)\n";

sub activate {
  my ($y) = @_;
  
  my $activate_y = 1 / ( 1 + exp(-$y));
  
  return $activate_y;
}
</pre>

必要な情報を、漏れなく書き出してみましょう。

入力の個数は、2個。

出力の個数は、3個。

必要な$wパラメーターの個数はいくつでしょうか。これは「入力個数「2」 * 出力個数「3」 = 6」です。

必要な$bパラメーターの個数はいくつでしょうか? 出力個数「3」と同じです。

入力は

<pre>
[$x0, $x1]
</pre>

パラメーター$wは、

<pre>
[
  $w00, $w01,
  $w10, $w11,
  $w20, $w21,
]
</pre>

パラメーター$bは、

<pre>
[
  $b0,
  $b1,
  $b2,
]
</pre>

受け取る出力は

<pre>
[$y0, $y1, $y2]
</pre>

です。

<h4>配列化</h4>

では、コーディングしてみます。最初は、配列化です。最初の例と、と同じ出力を得ることができれば、正解ですね。

目を凝らして、法則性を眺めてください。

<pre>
use strict;
use warnings;

my $x = [0.5, 0.8];
my $y = [0, 0, 0];
my $x_len = @$x;
my $y_len = @$y;

my $w = [
  0.1, 0.4,
  0.2, 0.5,
  0.3, 0.6,
];

my $b = [
  0.3,
  0.9,
  0.5,
];

$y->[0] = ($w->[$x_len * 0 + 0] * $x->[0]) + ($w->[$x_len * 0 + 1] * $x->[1]) + $b->[0];
$y->[1] = ($w->[$x_len * 1 + 0] * $x->[0]) + ($w->[$x_len * 1 + 1] * $x->[1]) + $b->[1];
$y->[2] = ($w->[$x_len * 2 + 0] * $x->[0]) + ($w->[$x_len * 2 + 1] * $x->[1]) + $b->[2];

$y->[0] = activate($y->[0]);
$y->[1] = activate($y->[1]);
$y->[2] = activate($y->[2]);

print "($y->[0], $y->[1], $y->[2])\n";

sub activate {
  my ($y) = @_;
  
  my $activate_y = 1 / ( 1 + exp(-$y));
  
  return $activate_y;
}
</pre>

出力結果は以下で、同じになりました。

<pre>
(0.661503159202952, 0.802183888558582, 0.755838899094797)
</pre>

<h4>ループ化</h4>

配列化が終わったら、次はループ化です。配列というデータ構造にすることで、ループのアルゴリズムを適用できます。二重ループで処理を書き直してみます。最初の例と、と同じ出力を得ることができれば、正解ですね。

<pre>
use strict;
use warnings;

my $x = [0.5, 0.8];
my $y = [0, 0, 0];
my $x_len = @$x;
my $y_len = @$y;

my $w = [
  0.1, 0.4,
  0.2, 0.5,
  0.3, 0.6,
];

my $b = [
  0.3,
  0.9,
  0.5,
];

for (my $y_index = 0; $y_index < $y_len; $y_index++) {
  my $total = 0;
  for (my $x_index = 0; $x_index < $x_len; $x_index++) {
    $total += ($w->[$x_len * $y_index + $x_index] * $x->[$x_index]);
  }
  $total +=  $b->[$y_index];
  $y->[$y_index] = activate($total);
}

print "($y->[0], $y->[1], $y->[2])\n";

sub activate {
  my ($y) = @_;
  
  my $activate_y = 1 / ( 1 + exp(-$y));
  
  return $activate_y;
}
</pre>

出力結果は以下で、同じになりました。

<pre>
(0.661503159202952, 0.802183888558582, 0.755838899094797)
</pre>

これで、mが2以外の数になったり、nが3以外の数になっても、対応できるようになりました。

ディープラーニングに必要な技術の中の、m個の入力をn個の出力に変換する方法の学習を終えました。
